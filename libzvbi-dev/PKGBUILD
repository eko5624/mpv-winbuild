pkgname=libzvbi-dev
pkgver=0.2.41
pkgrel=1
pkgdesc='A VBI decoding library which can be used by FFmpeg to decode DVB teletext pages and DVB teletext subtitles'
arch=('x86_64')
url="https://github.com/zapping-vbi/zvbi"
makedepends=('libiconv-dev' 'libpng-dev')
license=('BSD' 'GPL2' 'LGPL2.1' 'MIT')
source=("git+https://github.com/zapping-vbi/zvbi.git"
        "https://raw.githubusercontent.com/videolan/vlc/master/contrib/src/zvbi/zvbi-fix-static-linking.patch"
        "https://raw.githubusercontent.com/videolan/vlc/master/contrib/src/zvbi/zvbi-ioctl.patch"
        "https://raw.githubusercontent.com/videolan/vlc/master/contrib/src/zvbi/zvbi-ssize_max.patch"
        "https://raw.githubusercontent.com/videolan/vlc/master/contrib/src/zvbi/zvbi-win32-undefined.patch"
        "https://raw.githubusercontent.com/videolan/vlc/master/contrib/src/zvbi/zvbi-win32.patch"
        "https://raw.githubusercontent.com/shinchiro/mpv-winbuild-cmake/master/packages/libzvbi-0006-skip-directory.patch")
md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

build() {
  export PATH="${TOP_DIR}/cross/bin:$PATH"
  export PKG_CONFIG_LIBDIR="${MINGW_PREFIX}/lib/pkgconfig"
  export PKG_CONFIG="pkgconf --static"
  cd $srcdir/zvbi
  sed -i 's/configure.in/configure.ac/g' $srcdir/zvbi-fix-static-linking.patch
  for patch in $srcdir/*.patch; do
      echo "Applying $patch"
      patch -p1 < "$patch"
  done
  ./configure \
    --host=${TARGET_ARCH} \
    --prefix=${MINGW_PREFIX} \
    --disable-shared \
    --enable-static \
    --with-pic \
    --without-doxygen \
    --without-x \
    --disable-dvb \
    --disable-bktr \
    --disable-nls \
    --disable-proxy
  make -C src
}

package() {
  cd $srcdir/zvbi
  DESTDIR=$pkgdir make -C src install
  DESTDIR=$pkgdir make SUBDIRS=. install
}
