pkgname=(luajit2-shared-dev luajit2-shared)
pkgver=2.1_20231117
pkgrel=1
pkgdesc='Just-in-time compiler and drop-in replacement for Lua 5.1'
arch=('x86_64')
url="https://luajit.org/"
license=('MIT')
source=("git+https://github.com/openresty/luajit2")
md5sums=('SKIP')

build() {
  export PATH="${TOP_DIR}/cross/bin:$PATH"
  export PKG_CONFIG_LIBDIR="${MINGW_PREFIX}/lib/pkgconfig"
  export PKG_CONFIG="pkgconf --static"
  cd $srcdir/luajit2
  make -C src \
    CROSS=${TARGET_ARCH}- \
    TARGET_SYS=Windows \
    BUILDMODE=dynamic \
    FILE_T=luajit.exe \
    CFLAGS+=' -DUNICODE' \
    XCFLAGS='-DLUAJIT_ENABLE_LUA52COMPAT' \
    PREFIX=${MINGW_PREFIX} Q= \
    amalg 
}

package_luajit2-shared-dev() {
  cd $srcdir/luajit2
  make DESTDIR=$pkgdir \
    CROSS=${TARGET_ARCH}- \
    TARGET_SYS=Windows \
    BUILDMODE=dynamic \
    FILE_T=luajit.exe \
    CFLAGS+=' -DUNICODE' \
    XCFLAGS='-DLUAJIT_ENABLE_LUA52COMPAT' \
    PREFIX=${MINGW_PREFIX} Q= \
    install
  sed -i "s/-Wl,-E -lm -ldl/-lm/" $pkgdir${MINGW_PREFIX}/lib/pkgconfig/*.pc
  rm -rf $pkgdir${MINGW_PREFIX}/bin $pkgdir${MINGW_PREFIX}/share $pkgdir${MINGW_PREFIX}/lib/lua
  mv src/liblua*.a $pkgdir${MINGW_PREFIX}/lib
}

package_luajit2-shared() {
  export PKGEXT='.pkg.tar.xz'
  cd $srcdir/luajit2
  mkdir -p $pkgdir${MINGW_PREFIX}/bin
  mv src/lua*.exe $pkgdir${MINGW_PREFIX}/bin
  mv src/lua*.dll $pkgdir${MINGW_PREFIX}/bin
}
