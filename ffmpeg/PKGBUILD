pkgname=(ffmpeg-dev ffmpeg-git $([[ "$CLANG_PACKAGES_LTO" == "ON" ]] && echo "ffmpeg-thinlto"))
pkgver=7.1
pkgrel=1
pkgdesc='Complete and free Internet live audio and video solution'
arch=('x86_64')
url="http://ffmpeg.org/"
makedepends=("amf-headers-dev"
             "aom-dev"
             "avisynth-dev"
             "bzip2-dev"
             "dav1d-dev"
             "davs2-dev"
             "ffnvcodec-dev"
             "lame-dev"
             "lcms2-dev"
             "libaribcaption-dev"
             "libbs2b-dev"
             "libjxl-dev"
             "libmodplug-dev"
             "libmysofa-dev"
             "libogg-dev"
             "libopenmpt-dev"
             "libplacebo-dev"
             "libsdl2-dev"
             "libsoxr-dev"
             "libspeex-dev"
             "libsrt-dev"
             "libssh-dev"
             "libva-dev"
             "libvorbis_aotuv-dev"
             "libvpl-dev"
             "libvpx-dev"
             "libwebp-dev"
             "libxml2-dev"
             "libxvid-dev"
             "libzimg-dev"
             "openal-soft-dev"
             "openssl-dev"
             "opus-dev"
             "shaderc-dev"
             "svtav1-dev"
             "uavs3d-dev"
             "vapoursynth-dev")
license=('LGPL')
source=("git+https://github.com/FFmpeg/FFmpeg")
md5sums=('SKIP')

package_ffmpeg-dev() {
  cd $srcdir/FFmpeg
  # fix checks for small buffers
  curl -OL https://patch-diff.githubusercontent.com/raw/eko5624/FFmpeg/pull/2.patch
  patch -p1 -i 2.patch

  # --enable-libvvdec
  curl -OL https://raw.githubusercontent.com/wiki/fraunhoferhhi/vvdec/data/patch/v6-0001-avcodec-add-external-dec-libvvdec-for-H266-VVC.patch
  patch -p1 -i v6-0001-avcodec-add-external-dec-libvvdec-for-H266-VVC.patch

  # support hevc flv
  curl -OL https://raw.githubusercontent.com/yt-dlp/FFmpeg-Builds/master/patches/ffmpeg/master/0001-Nonstandard-HEVC-over-FLV.patch
  patch -p1 -i 0001-Nonstandard-HEVC-over-FLV.patch

  NO_CONFLTO=1 ./configure$FFMPEG_LTO \
    --cross-prefix=${TARGET_ARCH}- \
    --prefix=${MINGW_PREFIX} \
    --arch=x86_64 \
    --target-os=mingw32 \
    --target-exec=wine \
    --pkg-config-flags=--static \
    --enable-cross-compile \
    --enable-runtime-cpudetect \
    --disable-debug \
    --disable-doc \
    --enable-gpl \
    --enable-version3 \
    --enable-postproc \
    --disable-ffplay \
    --disable-ffprobe \
    --disable-avdevice \
    --disable-pthreads \
    --enable-w32threads \
    --extra-libs="${ffmpeg_extra_libs}" \
    --extra-ldflags="${ffmpeg_extra_ldflags}" \
    --disable-mediafoundation \
    --enable-amf \
    --enable-avisynth \
    --enable-cuvid${ffmpeg_cuda_llvm} \
    --enable-lcms2 \
    --enable-libaom \
    --enable-libaribcaption \
    --enable-libbs2b \
    --enable-libdav1d \
    --enable-libdavs2 \
    --enable-libjxl \
    --enable-libvpl \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libmysofa \
    --enable-libopenmpt \
    --enable-libopus \
    --enable-libplacebo \
    --enable-libsvtav1 \
    --enable-libshaderc \
    --enable-libsoxr \
    --enable-libspeex \
    --enable-libsrt \
    --enable-libssh \
    --enable-libuavs3d \
    --enable-libvorbis \
    --enable-libvpx \
    --enable-libvvdec \
    --enable-libwebp \
    --enable-libxml2 \
    --enable-libxvid \
    --enable-libzimg \
    --enable-libzvbi \
    --enable-openal \
    --enable-opengl \
    --enable-openssl \
    --enable-nvdec \
    --enable-nvenc \
    --enable-vaapi \
    --enable-vapoursynth \
    --disable-videotoolbox \
    --disable-decoder=libaom_av1 \
    --disable-decoder=libvorbis \
    --disable-decoder=libopus \
    --disable-encoder=opus \
    --disable-encoder=vorbis \
    --disable-vdpau
  make DESTDIR=$pkgdir install
  rm -rf $pkgdir${MINGW_PREFIX}/bin $pkgdir${MINGW_PREFIX}/share $pkgdir${MINGW_PREFIX}/lib/*.def
}

package_ffmpeg-git() {
  export PKGEXT='.pkg.tar.xz'
  mkdir -p $pkgdir${MINGW_PREFIX}/ffmpeg
  cd $srcdir/FFmpeg
  make DESTDIR=$pkgdir install
  rm -rf $pkgdir${MINGW_PREFIX}/bin/*.lib $pkgdir${MINGW_PREFIX}/share $pkgdir${MINGW_PREFIX}/include $pkgdir${MINGW_PREFIX}/lib
  strip $pkgdir${MINGW_PREFIX}/bin/*
  mv $pkgdir${MINGW_PREFIX}/bin/ffmpeg.exe $pkgdir${MINGW_PREFIX}/ffmpeg
  rm -rf $pkgdir${MINGW_PREFIX}/bin

  # get latest commit sha
  short_sha=$(git rev-parse --short HEAD)
  echo $short_sha > SHORT_SHA
  mv SHORT_SHA $pkgdir${MINGW_PREFIX}/ffmpeg
}

package_ffmpeg-thinlto() {
  export PKGEXT='.pkg.tar.zst'
  mkdir -p $pkgdir${TOP_DIR}
  mv ${TOP_DIR}/thinlto $pkgdir${TOP_DIR}
}
