pkgname=(libmpv-git mpv-git $([[ "$CLANG_PACKAGES_LTO" == "ON" ]] && echo "mpv-thinlto"))
pkgver=0.38.0
pkgrel=1
pkgdesc='a free, open source, and cross-platform media player'
arch=('x86_64')
url='https://mpv.io/'
makedepends=("libjpeg-dev"
             "libpng-dev"
             "libsdl2-dev"
             "libsixel-dev"
             "angle-headers-dev"
             "ffmpeg-dev"
             "mujs-dev"
             "rubberband-dev"
             "luajit2-dev" 
             "freetype2-dev"
             "fribidi-dev"
             "harfbuzz-dev"
             "libass-dev"
             "lcms2-dev"
             "libbluray-dev"
             "libdvdnav-dev"
             "libplacebo-dev"
             "shaderc-dev"
             "spirv-cross-dev"
             "vapoursynth-dev"
             "vulkan-dev"
             "zlib-dev")
license=('GPL3')
source=("git+https://github.com/mpv-player/mpv.git")
md5sums=('SKIP')

build() {
  cd $srcdir/mpv
  NO_CONFLTO=1 meson setup build \
    --prefix="${MINGW_PREFIX}" \
    --libdir=${MINGW_PREFIX}/lib \
    --cross-file=../../../cross.meson \
    -Doptimization=3 \
    -Db_lto=true \
    -Db_lto_mode=thin \
    -Dlibmpv=true \
    -Dlua=enabled \
    -Djavascript=enabled \
    -Dsdl2=enabled \
    -Dlibbluray=enabled \
    -Ddvdnav=enabled \
    -Drubberband=enabled \
    -Dlcms2=enabled \
    -Dopenal=enabled \
    -Dspirv-cross=enabled \
    -Dvulkan=enabled \
    -Dvapoursynth=enabled \
    -Degl-angle=enabled
  LTO_JOB=1 PDB=1 meson compile -C build
  
  # get latest commit sha
  short_sha=$(git rev-parse --short HEAD)
  echo $short_sha > SHORT_SHA
}

package_libmpv-git() {
  export PKGEXT='.pkg.tar.xz'
  mkdir -p $pkgdir${MINGW_PREFIX}/libmpv/include/mpv
  cd $srcdir/mpv/build
  ${TARGET_ARCH}-strip -s libmpv-2.dll
  mv libmpv-2.dll $pkgdir${MINGW_PREFIX}/libmpv
  mv libmpv.dll.a $pkgdir${MINGW_PREFIX}/libmpv
  mv ../libmpv/client.h $pkgdir${MINGW_PREFIX}/libmpv/include/mpv
  mv ../libmpv/stream_cb.h $pkgdir${MINGW_PREFIX}/libmpv/include/mpv
  mv ../libmpv/render.h $pkgdir${MINGW_PREFIX}/libmpv/include/mpv
  mv ../libmpv/render_gl.h $pkgdir${MINGW_PREFIX}/libmpv/include/mpv
}

package_mpv-git() {
  export PKGEXT='.pkg.tar.xz'
  mkdir -p $pkgdir${MINGW_PREFIX}/mpv
  cd $srcdir/mpv/build
  mv mpv.exe $pkgdir${MINGW_PREFIX}/mpv
  mv mpv.com $pkgdir${MINGW_PREFIX}/mpv
  ${TARGET_ARCH}-strip -s $pkgdir${MINGW_PREFIX}/mpv/mpv.*
  mv ../SHORT_SHA $pkgdir${MINGW_PREFIX}/mpv
}

package_mpv-thinlto() {
  export PKGEXT='.pkg.tar.zst'
  mkdir -p $pkgdir${TOP_DIR}
  mv ${TOP_DIR}/thinlto $pkgdir${TOP_DIR}
}
