pkgname=libjxl-dev
pkgver=0.8.2
pkgrel=1
pkgdesc='JPEG XL image format reference implementation'
arch=('x86_64')
url="https://github.com/libjxl/libjxl/"
makedepends=('brotli-dev' 'highway-dev' 'lcms2-dev')
license=('BSD')
source=("git+https://github.com/libjxl/libjxl")
md5sums=('SKIP')

build() {
  export PATH="${TOP_DIR}/cross/bin:$PATH"
  export PKG_CONFIG_LIBDIR="${MINGW_PREFIX}/lib/pkgconfig"
  export PKG_CONFIG="pkgconf --static"
  # Fix AVX2 related crash due to unaligned stack memory
  export CXXFLAGS="$CXXFLAGS -Wa,-muse-unaligned-vector-move"
  export CFLAGS="$CFLAGS -Wa,-muse-unaligned-vector-move"
 
  cd $srcdir/libjxl
  git submodule update --init --recursive --depth 1 --recommend-shallow third_party/libjpeg-turbo
  rm -rf build && mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_TOOLCHAIN_FILE=../../../toolchain.cmake \
    -DBUILD_SHARED_LIBS=OFF \
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON \
    -DJPEGXL_STATIC=ON \
    -DBUILD_TESTING=OFF \
    -DJPEGXL_EMSCRIPTEN=OFF \
    -DJPEGXL_BUNDLE_LIBPNG=OFF \
    -DJPEGXL_ENABLE_TOOLS=OFF \
    -DJPEGXL_ENABLE_VIEWERS=OFF \
    -DJPEGXL_ENABLE_DOXYGEN=OFF \
    -DJPEGXL_ENABLE_EXAMPLES=OFF \
    -DJPEGXL_ENABLE_MANPAGES=OFF \
    -DJPEGXL_ENABLE_JNI=OFF \
    -DJPEGXL_ENABLE_SKCMS=OFF \
    -DJPEGXL_ENABLE_PLUGINS=OFF \
    -DJPEGXL_ENABLE_DEVTOOLS=OFF \
    -DJPEGXL_ENABLE_BENCHMARK=OFF \
    -DJPEGXL_ENABLE_SJPEG=OFF  
  make
}

package() {
  cd $srcdir/libjxl/build
  DESTDIR=$pkgdir make install
}
