# Maintainer: nyfair <nyfair2012@gmail.com>
pkgname=vulkan-dev
pkgver=1.3.261.0
pkgrel=1
pkgdesc='Vulkan Header and Loader'
arch=('x86_64')
url="https://www.khronos.org/vulkan/"
license=('APACHE')
source=("git+https://github.com/KhronosGroup/Vulkan-Headers"
        "git+https://github.com/KhronosGroup/Vulkan-Loader"
        "https://raw.githubusercontent.com/shinchiro/mpv-winbuild-cmake/master/packages/vulkan-0001-cross-compile-static-linking-hacks.patch")
md5sums=('SKIP' 'SKIP' 'SKIP')

build() {
  export PATH="${TOP_DIR}/cross/bin:$PATH"
  export PKG_CONFIG_LIBDIR="${MINGW_PREFIX}/lib/pkgconfig"
  export PKG_CONFIG="pkgconf --static"

  export CFLAGS="${CFLAGS} -D_WIN32_WINNT=0x0600 -D__STDC_FORMAT_MACROS -DSTRSAFE_NO_DEPRECATE -Dparse_number=cjson_parse_number"
  export CPPFLAGS="${CPPFLAGS} -D__STDC_FORMAT_MACROS"
  export CXXFLAGS="${CXXFLAGS} -D__USE_MINGW_ANSI_STDIO -D__STDC_FORMAT_MACROS -fpermissive -Wno-unused-function"

  cd $srcdir/Vulkan-Headers
  rm -rf build && mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_TOOLCHAIN_FILE=../../../toolchain.cmake \
    -DCMAKE_BUILD_TYPE=Release
  cmake --install . --prefix=$srcdir/opt

  cd $srcdir/Vulkan-Loader
  patch -p1 -i $srcdir/vulkan-0001-cross-compile-static-linking-hacks.patch
  rm -rf build && mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_TOOLCHAIN_FILE=../../../toolchain.cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DVULKAN_HEADERS_INSTALL_DIR="${srcdir}/opt" \
    -DBUILD_STATIC_LOADER=ON \
    -DBUILD_TESTS=OFF \
    -DENABLE_WERROR=OFF
  make
}

package() {
  cd $srcdir/Vulkan-Headers/build
  make DESTDIR=$pkgdir install
  cd $srcdir/Vulkan-Loader/build
  make DESTDIR=$pkgdir install
}
