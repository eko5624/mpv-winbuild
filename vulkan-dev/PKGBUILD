pkgname=vulkan-dev
pkgver=1.3.280.0
pkgrel=1
pkgdesc='Vulkan Header and Loader'
arch=('x86_64')
url="https://www.khronos.org/vulkan/"
license=('APACHE')
source=("git+https://github.com/KhronosGroup/Vulkan-Headers"
        "git+https://github.com/KhronosGroup/Vulkan-Loader"
        "https://raw.githubusercontent.com/shinchiro/mpv-winbuild-cmake/master/packages/vulkan-0001-cross-compile-static-linking-hacks.patch")
md5sums=('SKIP' 'SKIP' 'SKIP')

build() {
  export CFLAGS+=" -D__STDC_FORMAT_MACROS -DSTRSAFE_NO_DEPRECATE -Dparse_number=cjson_parse_number"
  export CPPFLAGS+=" -D__STDC_FORMAT_MACROS"
  export CXXFLAGS+=" -D__STDC_FORMAT_MACROS -fpermissive"

  cd $srcdir/Vulkan-Headers
  rm -rf build && mkdir build && cd build
  NO_CONFLTO=1 cmake .. \
    -G "Ninja" \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_TOOLCHAIN_FILE=../../../toolchain.cmake \
    -DCMAKE_BUILD_TYPE=Release
  cmake --install . --prefix=$srcdir/opt

  cd $srcdir/Vulkan-Loader
  patch -p1 -i $srcdir/vulkan-0001-cross-compile-static-linking-hacks.patch
  rm -rf build && mkdir build && cd build
  NO_CONFLTO=1 cmake .. \
    -G "Ninja" \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_TOOLCHAIN_FILE=../../../toolchain.cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DVULKAN_HEADERS_INSTALL_DIR="${srcdir}/opt" \
    -DBUILD_TESTS=OFF \
    -DENABLE_WERROR=OFF \
    -DUSE_MASM=OFF \
    -DUSE_GAS=OFF \
    -DBUILD_STATIC_LOADER=ON
  cmake --build .
}

package() {
  mkdir -p $pkgdir${MINGW_PREFIX}/lib/pkgconfig
  cd $srcdir/Vulkan-Headers/build
  DESTDIR=$pkgdir cmake --install .
  cd $srcdir/Vulkan-Loader/build
  cp loader/libvulkan.a $pkgdir${MINGW_PREFIX}/lib
  cp loader/vulkan_own.pc $pkgdir${MINGW_PREFIX}/lib/pkgconfig/vulkan.pc
}
