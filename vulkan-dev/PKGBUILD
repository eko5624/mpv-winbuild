# Maintainer: nyfair <nyfair2012@gmail.com>
pkgname=vulkan-shared-dev
pkgver=1.3.268.0
pkgrel=1
pkgdesc='Vulkan Header and Loader'
arch=('x86_64')
url="https://www.khronos.org/vulkan/"
license=('APACHE')
source=("git+https://github.com/KhronosGroup/Vulkan-Headers#tag=sdk-$pkgver"
        "git+https://github.com/KhronosGroup/Vulkan-Loader#tag=sdk-$pkgver")
md5sums=('SKIP' 'SKIP')

build() {
  export PATH="${TOP_DIR}/cross/bin:$PATH"
  export PKG_CONFIG_LIBDIR="${MINGW_PREFIX}/lib/pkgconfig"
  export PKG_CONFIG="pkgconf --static"

  export CFLAGS="${CFLAGS} -D__STDC_FORMAT_MACROS -Wno-unused-function"
  export CPPFLAGS="${CPPFLAGS} -D__STDC_FORMAT_MACROS"
  export CXXFLAGS="${CXXFLAGS} -D__USE_MINGW_ANSI_STDIO -D__STDC_FORMAT_MACROS -fpermissive -Wno-unused-function"

  cd $srcdir/Vulkan-Headers
  rm -rf build && mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_TOOLCHAIN_FILE=../../../toolchain.cmake \
    -DCMAKE_BUILD_TYPE=Release
  cmake --install . --prefix=$srcdir/opt

  cd $srcdir/Vulkan-Loader

  # skip rc info and use vulkan-1.dll from your graphic driver
  sed -i "/loader.rc/d" loader/CMakeLists.txt
  sed -i "/RC_FILE_LOCATION/d" loader/CMakeLists.txt
  sed -i "s/vulkan-1.def/vulkan-1.def)/" loader/CMakeLists.txt

  rm -rf build && mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_TOOLCHAIN_FILE=../../../toolchain.cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DVULKAN_HEADERS_INSTALL_DIR="${srcdir}/opt" \
    -DBUILD_TESTS=OFF \
    -DUSE_MASM=OFF \
    -DENABLE_WERROR=OFF
  make
}

package() {
  cd $srcdir/Vulkan-Headers/build
  make DESTDIR=$pkgdir install
  cd $srcdir/Vulkan-Loader/build
  make DESTDIR=$pkgdir install
  strip $pkgdir${MINGW_PREFIX}/bin/*
}
